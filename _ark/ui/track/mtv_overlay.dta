(RndDir
   (types
      (mtv_overlay
         (editor
            (show
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim set_frame 50}}))
            (hide
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim set_frame 0}}))
            (play
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim animate}})))
         (enter
            {if
               {exists game}
               {game
                  add_sink
                  $this
                  (intro_end)}
               {$this setup_text}
               {if
                  {exists fade.anim}
                  {fade.anim set_frame 0}}})
         (exit
            {if
               {exists game}
               {game remove_sink $this}})
         (intro_end
            {if
               {&&
                  {exists game}
                  {gamemode get show_mtv_overlay}}
               {$this setup_text}
               {if
                  {exists fade.anim}
                  {if_else $songtitle
                  {fade.anim set_frame 50}
                  {fade.anim animate}}}})
         (setup_text
            {if
               {exists song.lbl}
               {do
               ($song
                  {meta_performer song})
               ($song_text
                  {song_mgr song_name $song})
               ($artist
                     {sprint "\n" {meta_performer song_artist}})
               {if 
                  {||
                     ; disables artist line if the artist is green day and only show non-gd artists modifier is on
                     ; show all artists modifier takes priority and will keep the artist line
                     {&& {== {meta_performer song_artist} "Green Day"} $forceartist {! $showartist}}
                     ; disables artist line if both show all artists and only show non-gd artists modifiers are off
                     {&& {! $forceartist} {! $showartist}}
                  }
                  {set $artist ""}  
               }
               {song.lbl set fixed_length 1000}
               {if_else {!= $speedmod 1}
				   {song.lbl set_display_text {sprint $song_text " (" {int {'+' 0.5 {'*' $speedmod 100}}} "%)" $artist}}
               {song.lbl set_display_text {sprint $song_text $artist}}}}}))))